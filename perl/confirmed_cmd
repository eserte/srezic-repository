=head2 confirmed_cmd($code, $label)

=for category System

Wait for user confirmation, then execute the given code reference. The
source code of the code reference is presented to the user, unless the
option $label is given.

Recommendation: also use

    use autodie qw(:default :system);

=cut

sub confirmed_cmd (&;$) {
    my($code, $label) = @_;
    require B::Deparse;
    if (!defined $label) {
	my $pad_defs = "# cannot get lexicals, no PadWalker installed\n";
	if (eval { require PadWalker; require Data::Dumper; 1 }) {
	    my $list = PadWalker::closed_over($code);
	    my @pad_defs;
	    while(my($k,$v) = each %$list) {
		push @pad_defs, Data::Dumper->new([$$v],[$k])->Indent(1)->Useqq(1)->Dump;
	    }
	    $pad_defs = @pad_defs ? "\n" . join("", map { "    $_" } @pad_defs) : '';
	    $pad_defs =~ s{\n$}{};
	}
	$label = B::Deparse->new->coderef2text($code);
	$label =~ s{^\s*use strict 'refs';\s*\n}{}m;
	$label =~ s<^{><{$pad_defs>;
    }
    print STDERR "CALL $label ";
    <STDIN>;
    $code->();
}
