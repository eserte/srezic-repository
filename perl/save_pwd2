=head2 save_pwd2

=for category File

Save the current directory and restore it until the remembered object
gets out of scope.

=cut

BEGIN {
    sub save_pwd2 {
	require Cwd;
	my $pwd = Cwd::getcwd();
	$pwd = undef if defined $pwd && $pwd eq ''; # might be consinstent in older Cwd versions 
	if (!defined $pwd) {
	    warn "No known current working directory";
	}
	bless {cwd => $pwd}, __PACKAGE__ . '::SavePwd2';
    }
    my $DESTROY = sub {
	my $self = shift;
	if (defined $self->{cwd}) {
	    chdir $self->{cwd}
	        or die "Can't chdir back to $self->{cwd}: $!";
	}
    };
    no strict 'refs';
    *{__PACKAGE__.'::SavePwd2::DESTROY'} = $DESTROY;
}
